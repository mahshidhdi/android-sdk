// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.21'

    repositories {
        google()
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.0.1'
        classpath 'io.sentry:sentry-android-gradle-plugin:1.7.5'
        classpath 'digital.wup:android-maven-publish:3.6.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
        classpath 'com.github.triplet.gradle:play-publisher:2.2.0-SNAPSHOT'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }

    ext {
        release = [
            versionName: '1.0.4',
            versionCode: 100004
        ]

        setup = [
                compileSdk: 28,
                minSdk    : 15,
                targetSdk : 28
        ]

        versions = [
            rxJava: '2.2.8',
            dagger: '2.23',
            workManager: '1.0.1',
            moshi: '1.7.0',
            sentry: '1.7.10',
            firebaseCore: '16.0.9',
            firebaseMessaging: '17.6.0',
            mockk: '1.9.3',
            gms: '16.+',
            room: '1.1.1-rc1'
        ]

        proguard = [
            common: "$rootProject.rootDir.absolutePath/proguard-common.pro",
            consumerCommon: "$rootProject.rootDir.absolutePath/proguard-consumer-common.pro"
        ]
    }

    ext {
        bintrayRepo = 'hengam'
        publishedGroupId = 'io.hengam.lib'
        siteUrl = 'https://hengam.io'
        gitUrl = 'https://github.com/hengam-io/android-sdk'

        licenseName = 'The Apache Software License, Version 2.0'
        licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        allLicenses = ["Apache-2.0"]

        uploadJavadocs = false
        uploadSources = false
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven {
            url "https://repo.eclipse.org/content/repositories/paho-snapshots/"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    project.configurations.all { config ->
        if (config.name.contains("test") || config.name.contains("Test")) {
            resolutionStrategy.dependencySubstitution {
                substitute project(":rxjava") because "we use a non-minified rxjava for tests" with module("io.reactivex.rxjava2:rxjava:$versions.rxJava")
            }
        }
    }
}